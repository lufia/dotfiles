#!/bin/awk -f

FILENAME != filename {
	print "---"
	filename = FILENAME
	match($0, /^[ \t]+/)
	indent = RSTART+RLENGTH
}
{	s = substr($0, indent)
	match(s, /^[ \t]*/)
	s = substr(s, RSTART+RLENGTH)
	n = RSTART+RLENGTH-1 # markdown list indent

	if(match(s, /^\[([a-zA-Z0-9_-]+)\.icon\] ?/, a) > 0)
		s = sprintf("**%s**: %s", a[1], substr(s, RSTART+RLENGTH))
	if(match(s, /^\[([^\]]+) (https?:\/\/.*)]/, a) > 0){
		s = sprintf("%s[%s](%s)%s", substr(s, 1, RSTART-1), a[1], a[2], substr(s, RSTART+RLENGTH))
	}

	if(s == ""){
		print ""
		next
	}
	if(s ~ /^code:/){
		print "```" substr(s, 6)
		next
	}
	if(s ~ /^\[\*/){
		print title(s)
		print ""
		next
	}
	printf "%*s* %s\n", n*2, "", s
}

function title(s) {
	s = substr(s, 2, length(s)-2)
	if(substr(s, 1, 4) == "*** ")
		return "## " substr(s, 5)
	if(substr(s, 1, 3) == "** ")
		return "### " substr(s, 4)
	if(substr(s, 1, 2) == "* ")
		return "#### " substr(s, 3)
}
